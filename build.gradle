import com.vanniktech.maven.publish.SonatypeHost

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext {
    junit_androidx_version = '1.1.5'
    junit_version = '4.13.2'
    moshi_version = '1.12.0'
    lifecycle_version = '2.5.1'
    mockk_version = '1.12.3'
    coroutines_test_version = '1.6.4'
    robolectric_version = '4.9.2'
    mockwebserver_version = '4.10.0'
    google_truth_version = '1.1.3'
    startup_version = '1.1.1'
    okhttp_version = '3.14.9'

    versions = [
        'minSdk': 21,
        'compileSdk': 31,
        'targetSdk': 31,
    ]
  }
}

plugins {
  id 'com.android.application' version '7.4.2' apply false
  id 'com.android.library' version '7.4.2' apply false
  id 'org.jetbrains.kotlin.android' version '1.5.30' apply false
  id "com.diffplug.spotless" version "6.17.0"
  id "com.vanniktech.maven.publish.base" version "0.25.1"
}

subprojects { subproject ->
  apply plugin: "com.diffplug.spotless"
  spotless {
    kotlin {
      target("src/**/*.kt")
      // ktlint doesn't honour .editorconfig yet: https://github.com/diffplug/spotless/issues/142
      ktlint('0.48.2').editorConfigOverride([
          'insert_final_newline': 'true',
          'end_of_line': 'lf',
          'charset': 'utf-8',
          'indent_size': '2',
      ])
      licenseHeaderFile(rootProject.file('gradle/license-header.txt'))
    }
    java {
      target("src/**/*.java")
    }
  }
}
def NEXT_VERSION = "2.0.16-SNAPSHOT"

allprojects {
  group = 'app.cash.paykit'
  version = '2.0.15-SNAPSHOT'

  plugins.withId("com.vanniktech.maven.publish.base") {
    mavenPublishing {
      publishToMavenCentral(SonatypeHost.DEFAULT, true)
      signAllPublications()
      pom {
        description.set("Cash App Pay SDK")
        name.set(project.name)
        url.set("https://github.com/cashapp/android-cash-paykit-sdk/")
        licenses {
          license {
            name.set("The Apache Software License, Version 2.0")
            url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
            distribution.set("repo")
          }
        }
        scm {
          url.set("https://github.com/cashapp/android-cash-paykit-sdk/")
          connection.set("scm:git:git://github.com/cashapp/android-cash-paykit-sdk.git")
          developerConnection
              .set("scm:git:ssh://git@github.com/cashapp/android-cash-paykit-sdk.git")
        }
        developers {
          developer {
            id.set("square")
            name.set("Square, Inc.")
          }
        }
      }
    }
  }
}