import com.vanniktech.maven.publish.SonatypeHost

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext {
    junit_androidx_version = '1.1.5'
    junit_version = '4.13.2'
    moshi_version = '1.15.2'

    // Raising lifecycle to 2.10.0 will require us to raise minSdk to 23
    // ( https://developer.android.com/jetpack/androidx/releases/lifecycle#2.10.0 )
    lifecycle_version = '2.9.0'
    mockk_version = '1.14.9'
    coroutines_test_version = '1.9.0'
    robolectric_version = '4.15.1'
    google_truth_version = '1.4.5'
    startup_version = '1.2.0'
    okhttp_version = '5.3.2'
    kotlinx_date_version = '0.6.2'

    versions = [
        'minSdk': 21,
        'compileSdk': 36,
    ]
  }
}

plugins {
  id 'com.android.application' version '8.13.2' apply false
  id 'com.android.library' version '8.13.2' apply false
  id 'org.jetbrains.kotlin.android' version '2.2.21' apply false
  id "org.jetbrains.kotlinx.binary-compatibility-validator" version "0.18.1" apply false
  id "com.diffplug.spotless" version "8.2.0"
  id "com.vanniktech.maven.publish.base" version "0.32.0"
}

subprojects { subproject ->
  apply plugin: "com.diffplug.spotless"
  spotless {
    kotlin {
      target("src/**/*.kt")
      ktlint('1.8.0').editorConfigOverride([
          'insert_final_newline': 'true',
          'end_of_line': 'lf',
          'charset': 'utf-8',
          'indent_size': '2',
          'ktlint_standard_max-line-length': 'disabled',
      ])
      licenseHeaderFile(rootProject.file('gradle/license-header.txt'))
    }
    java {
      target("src/**/*.java")
    }
  }
}

allprojects {

  group = 'app.cash.paykit'
  version = '2.6.1-SNAPSHOT'

  plugins.withId("com.vanniktech.maven.publish.base") {
    apply plugin: "binary-compatibility-validator"
    mavenPublishing {
      publishToMavenCentral(SonatypeHost.DEFAULT, true)
      signAllPublications()
      pom {
        description.set("Cash App Pay SDK")
        name.set(project.name)
        url.set("https://github.com/cashapp/android-cash-paykit-sdk/")
        licenses {
          license {
            name.set("The Apache Software License, Version 2.0")
            url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
            distribution.set("repo")
          }
        }
        scm {
          url.set("https://github.com/cashapp/android-cash-paykit-sdk/")
          connection.set("scm:git:git://github.com/cashapp/android-cash-paykit-sdk.git")
          developerConnection
              .set("scm:git:ssh://git@github.com/cashapp/android-cash-paykit-sdk.git")
        }
        developers {
          developer {
            id.set("square")
            name.set("Square, Inc.")
          }
        }
      }
    }
  }
}