import com.vanniktech.maven.publish.SonatypeHost
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext {
    junit_androidx_version = '1.1.5'
    junit_version = '4.13.2'
    moshi_version = '1.15.0'
    lifecycle_version = '2.6.2'
    mockk_version = '1.13.5'
    coroutines_test_version = '1.7.3'
    robolectric_version = '4.11.1'
    google_truth_version = '1.2.0'
    startup_version = '1.1.1'
    okhttp_version = '4.12.0'
    kotlinx_date_version = '0.4.1'

    versions = [
        'minSdk': 21,
        'compileSdk': 33,
        'targetSdk': 33,
    ]
  }
}

plugins {
  id 'com.android.application' version '8.2.2' apply false
  id 'com.android.library' version '8.2.2' apply false
  id 'org.jetbrains.kotlin.android' version '1.8.22' apply false
  id "com.diffplug.spotless" version "6.20.0"
  id "com.vanniktech.maven.publish.base" version "0.25.1"
}

subprojects { subproject ->
  apply plugin: "com.diffplug.spotless"
  spotless {
    kotlin {
      target("src/**/*.kt")
      // ktlint doesn't honour .editorconfig yet: https://github.com/diffplug/spotless/issues/142
      ktlint('0.49.1').editorConfigOverride([
          'insert_final_newline': 'true',
          'end_of_line': 'lf',
          'charset': 'utf-8',
          'indent_size': '2',
      ])
      licenseHeaderFile(rootProject.file('gradle/license-header.txt'))
    }
    java {
      target("src/**/*.java")
    }
  }
}

def NEXT_VERSION = "2.4.1-SNAPSHOT"

allprojects {

  group = 'app.cash.paykit'
  version = '2.4.0-SNAPSHOT'

  plugins.withId("com.vanniktech.maven.publish.base") {
    mavenPublishing {
      publishToMavenCentral(SonatypeHost.DEFAULT, true)
      signAllPublications()
      pom {
        description.set("Cash App Pay SDK")
        name.set(project.name)
        url.set("https://github.com/cashapp/android-cash-paykit-sdk/")
        licenses {
          license {
            name.set("The Apache Software License, Version 2.0")
            url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
            distribution.set("repo")
          }
        }
        scm {
          url.set("https://github.com/cashapp/android-cash-paykit-sdk/")
          connection.set("scm:git:git://github.com/cashapp/android-cash-paykit-sdk.git")
          developerConnection
              .set("scm:git:ssh://git@github.com/cashapp/android-cash-paykit-sdk.git")
        }
        developers {
          developer {
            id.set("square")
            name.set("Square, Inc.")
          }
        }
      }
    }
  }
}