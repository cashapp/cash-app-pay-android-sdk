plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id("com.google.devtools.ksp").version("2.2.21-2.0.4")
  id 'project-report' // run ./gradlew htmlDependencyReport
  id "com.vanniktech.maven.publish.base"
}

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier.set('sources')
}

android {
  namespace 'app.cash.paykit.core'
  compileSdk versions.compileSdk

  defaultConfig {
    minSdk versions.minSdk

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
    resValue("string", "cap_version", "$version")
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = "17"
  }

  kotlin {
    jvmToolchain(17)
  }

  resourcePrefix 'cap_'

  lintOptions {
    abortOnError true
    htmlReport true
    checkAllWarnings true
    warningsAsErrors true
    baseline file("lint-baseline.xml")
  }

  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }

  buildFeatures {
    buildConfig = true
  }
}

dependencies {

  // We want to lock this dependency at a lower than latest version to not force transitive updates onto merchants.
  //noinspection GradleDependency
  ksp("com.squareup.moshi:moshi-kotlin-codegen:$moshi_version")
  //noinspection GradleDependency
  implementation("com.squareup.moshi:moshi-kotlin:$moshi_version")

  // KotlinX Dates.
  implementation "org.jetbrains.kotlinx:kotlinx-datetime:$kotlinx_date_version"

  // Provides a lifecycle for the whole application process.
  implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

  // AndroidX Startup.
  implementation "androidx.startup:startup-runtime:$startup_version"

  //noinspection GradleDependency
  implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

  implementation project(':logging')
  implementation project(':analytics-core')

  // TEST RELATED.

  testImplementation "junit:junit:$junit_version"
  testImplementation "io.mockk:mockk:$mockk_version"
  testImplementation "com.google.truth:truth:$google_truth_version"
  androidTestImplementation "androidx.test.ext:junit-ktx:$junit_androidx_version"
  testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
  // Robolectric environment.
  testImplementation "org.robolectric:robolectric:$robolectric_version"
  // Coroutines test support.
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
  //Test helpers for Lifecycle runtime
  testImplementation "androidx.lifecycle:lifecycle-runtime-testing:$lifecycle_version"
}

mavenPublishing {
  // AndroidMultiVariantLibrary(publish a sources jar, publish a javadoc jar)
  configure(new com.vanniktech.maven.publish.AndroidSingleVariantLibrary("release", true, true))
}